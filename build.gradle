buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:0.8.1527"
        classpath 'de.undercouch:gradle-download-task:1.0'
    }
}

plugins {
    id 'com.github.jk1.tcdeps' version '0.2'
}

ext.kotlinVersion = "0.8.1527"
ext.ideaVersion = "13"
ext.ideaPath = file("$buildDir/download/idea")
ext.ideaArtifactName = "ideaIC-139.SNAPSHOT.win"

allprojects {
    apply plugin: 'kotlin'

    repositories {
        mavenCentral()
        teamcityServer {
            url = 'https://teamcity.jetbrains.com'
        }
    }

    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    dependencies {
        compile 'org.jetbrains:annotations:13.0'
    }
}

project(':metadata-model') {
    dependencies {
        compile 'com.thoughtworks.xstream:xstream:1.4.7'
    }
}

project(':metadata-crawler') {
    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    dependencies {
        compile project(':metadata-model')

        compile 'commons-io:commons-io:2.4'
        compile 'org.apache.commons:commons-lang3:3.3.2'
        compile 'net.htmlparser.jericho:jericho-html:3.3'
        compile 'org.jsoup:jsoup:1.8.1'
    }
}

repositories {
    ivy {
        url = "https://teamcity.jetbrains.com/guestAuth/repository/download"
        layout 'pattern', {
            artifact '[module]/[revision]/[artifact](.[ext])'
            ivy '[module]/[revision]/teamcity-ivy.xml'
        }
    }
}

configurations { ivydeps }

dependencies {
    compile project(':metadata-model')

    compile fileTree(dir: new File(ideaPath, "lib"), include: '*.jar')
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    ivydeps tc("bt410:183846.tcbuildid:${ideaArtifactName}.zip")

    testCompile fileTree(dir: "${System.properties['java.home']}/../lib", include: '*tools.jar')
}

task extractIdea(type: Copy) {
    from zipTree(configurations.ivydeps.files.find { it.name.startsWith(ideaArtifactName) })
    into ideaPath
}

compileKotlin.dependsOn(extractIdea)
compileJava.dependsOn(extractIdea)

task zip(type: Zip) {
    baseName 'CloudFormation'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    into('CloudFormation/lib') {
        from configurations.compile.files.findAll {
            // Filter out IntelliJ IDEA files
            File parent = it.getParentFile()
            while (parent != null) {
                if (parent.equals(ideaPath))
                    return false;
                parent = parent.getParentFile();
            }
            return true;
        }
        from configurations.runtime.artifacts.files
    }
}

artifacts {
    archives zip
}

//task ss(dependsOn: [configurations.compile, configurations.runtime, configurations.archives]) << {
//    // configurations.runtime.allArtifacts.files.each { File file -> println "xxx " + file }
//    configurations.compile.files.each { xxx -> println "compile " + xxx }
//    println new File(ideaPath, "lib")
//
//    println "\nconfigurations.compile.allArtifacts.files = ${configurations.compile.allArtifacts.files}"
//    println "\nconfigurations.compile.artifacts.files = ${configurations.compile.artifacts.files.asPath}"
//
//    println "\nconfigurations.runtime.allArtifacts.files = ${configurations.runtime.allArtifacts.files}"
//    println "\nconfigurations.runtime.artifacts.files = ${configurations.runtime.artifacts.files.asPath}"
//
//    println "\nconfigurations.archives.allArtifacts.files = ${configurations.archives.allArtifacts.files}"
//    println "\nconfigurations.archives.artifacts.files = ${configurations.archives.artifacts.files.asPath}"
//}
